/* nf-denovoslim — nextflow.config */

params {
    // --- Required inputs (must be supplied on CLI or in run scripts) ---
    trinity_fasta        = null
    samplesheet          = null
    species_label        = 'species_X'
    diamond_db           = null
    busco_lineage        = null

    // SortMeRNA rRNA databases
    sortmerna_db_urls    = [
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5.8s-database-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5s-database-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-arc-16s-id95.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-arc-23s-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-bac-16s-id90.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-bac-23s-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-euk-18s-id95.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-euk-28s-id98.fasta'
    ]
    sortmerna_db_dir     = null

    // MMseqs2 databases
    mmseqs2_swissprot    = null
    mmseqs2_pfam         = null
    mmseqs2_eggnog       = null
    mmseqs2_taxonomy_db  = null

    // Resource caps (overridden by site configs / profiles)
    max_cpus             = 16
    max_memory           = 128.GB
    max_time             = 168.h   // 7 days

    // Output directory (default; overridden by --outdir on CLI)
    outdir               = './results'

    // --- Pipeline params used in config closures ---
    // TD2 ORF prediction
    td2_strategy         = null     // null | 'conservative' | 'standard' | 'aggressive'
    td2_min_orf_length   = 90       // -m: min ORF length (aa) for long transcripts
    td2_abs_min_orf      = 50       // -M: absolute min ORF length (aa) for short transcripts
    td2_length_scale     = 0.5      // -L: accept short ORF if it covers >= this fraction of transcript
    td2_strand_specific  = true

    // MMseqs2 search sensitivity
    mmseqs2_search_sens  = 7.0

    // Taxonomy filter (NCBI taxon ID)
    filter_taxon         = 33090         // Viridiplantae

    // Merge prediction params (TD2 + MetaEuk + GeneMarkS-T)
    min_psauron          = 0.3           // minimum PSAURON score for merged predictions

    // Cluster / site (nullable — closures handle null via ternary)
    unix_group           = null
    orion_exclude_nodes  = null          // e.g. 'cn-37'
}

// Container images — not user-facing params.
// Docker-format URIs; auto-converted for Singularity/Apptainer.
def img = [
    sortmerna  : 'community.wave.seqera.io/library/sortmerna:4.3.7--b730cad73fc42b8e',
    salmon     : 'quay.io/biocontainers/salmon:1.10.3--h6dccd9a_2',
    mmseqs2    : 'quay.io/biocontainers/mmseqs2:18.8cc5c--hd6d6fdc_0',
    corset     : "${projectDir}/containers/corset/corset_1.10.sif",
    metaeuk    : 'quay.io/biocontainers/metaeuk:7.bba0d80--pl5321hd6d6fdc_2',
    td2        : "${projectDir}/containers/td2/td2_1.0.8.sif",
    busco      : 'ezlabgva/busco:v6.0.0_cv1',
    transannot : "${projectDir}/containers/transannot/transannot_4.0.0_eggnog7.sif",
    diamond    : 'quay.io/biocontainers/diamond:2.1.22--h13889ed_0',
    biopython  : 'quay.io/biocontainers/biopython:1.81',
    gmst       : "${projectDir}/containers/gmst/gmst.sif",
]

process {
    shell = ['/bin/bash', '-euo', 'pipefail']

    withName: 'SORTMERNA.*'           { container = img.sortmerna }
    withName: 'MMSEQS2_.*'            { container = img.mmseqs2 }
    withName: 'SALMON_.*'             { container = img.salmon }
    withName: 'CORSET'                { container = img.corset }
    withName: 'SELECT_REP'            { container = img.biopython }
    withName: 'DIAMOND_BLASTX.*'      { container = img.diamond }
    withName: 'CORRECT_FRAMESHIFTS.*' { container = img.biopython }
    withName: 'TD2_.*'                { container = img.td2 }
    withName: 'METAEUK_PREDICT'       { container = img.metaeuk }
    withName: 'METAEUK_SELECT_BEST'   { container = img.biopython }
    withName: 'PSAURON_METAEUK'       { container = img.td2 }
    withName: 'GMST_PREDICT'          { container = img.gmst }
    withName: 'PSAURON_GMST'          { container = img.td2 }
    withName: 'BUSCO_.*'              { container = img.busco }
    withName: 'TRANSANNOT'            { container = img.transannot }
    withName: 'SELECT_BEST_ORF'       { container = img.biopython }
    withName: 'MERGE_PREDICTIONS'     { container = img.biopython }
    withName: 'DECONTAMINATE_TRINITY' { container = img.biopython }
    withName: 'VALIDATE_IDS'          { container = 'ubuntu:22.04' }
    withName: 'THINNING_REPORT'       { container = img.biopython }
}

// Load base resource config
includeConfig 'conf/base.config'

// Usage: -profile apptainer,orion | apptainer,slurm | docker
profiles {
    apptainer {
        apptainer.enabled     = true
        apptainer.autoMounts  = true
        apptainer.cacheDir    = "${projectDir}/singularity_cache"
        apptainer.pullTimeout = '45m'
        docker.enabled        = false
    }
    singularity {
        singularity.enabled    = true
        singularity.autoMounts = true
        singularity.cacheDir   = "${projectDir}/singularity_cache"
        docker.enabled         = false
    }
    slurm {
        process.executor         = 'slurm'
        executor.submitRateLimit = '30/1min'
        executor.queueSize       = 20
    }
    orion {
        // NMBU Orion HPC — loads site-specific config
        // Set params.unix_group and params.orion_exclude_nodes on CLI or in run scripts
        includeConfig 'conf/orion.config'
    }
    docker {
        docker.enabled    = true
        docker.runOptions = '-u $(id -u):$(id -g)'
    }
    // Resource profiles — pick one to match your cluster
    highmem {
        // Large-memory nodes (>512 GB RAM, e.g. Orion)
        params.max_cpus   = 64
        params.max_memory = 1200.GB
        params.max_time   = 336.h
    }
    standard {
        // Mid-range cluster nodes (128-256 GB RAM)
        params.max_cpus   = 16
        params.max_memory = 128.GB
        params.max_time   = 168.h
    }
    test {
        // Minimal test run with low resources
        params.max_cpus   = 4
        params.max_memory = 16.GB
        params.max_time   = 6.h
    }
}

// --- Manifest ---
manifest {
    name            = 'nf-denovoslim'
    author          = 'Martin Paliocha'
    description     = 'Trinity assembly thinning pipeline'
    mainScript      = 'main.nf'
    nextflowVersion = '!>=25.10.0'
    version         = '0.1.0'
}
