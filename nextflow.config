/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Trinity Assembly Thinning Pipeline — nextflow.config
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

params {
    // --- Input ---
    trinity_fasta       = null
    samplesheet         = null
    species_label       = 'species_X'

    // --- SortMeRNA rRNA databases ---
    sortmerna_db_urls   = [
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5.8s-database-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5s-database-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-arc-16s-id95.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-arc-23s-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-bac-16s-id90.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-bac-23s-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-euk-18s-id95.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-euk-28s-id98.fasta'
    ]
    sortmerna_db_dir    = null   // Pre-downloaded dir; if null, downloads from URLs

    // --- Databases (pre-built MMseqs2) ---
    mmseqs2_swissprot   = '/mnt/project/glowberry/transannot/db/SwissProtDB'
    mmseqs2_pfam        = '/mnt/project/glowberry/transannot/db/PfamDB'
    mmseqs2_eggnog      = '/mnt/project/glowberry/transannot/db/eggNOG7DB'
    mmseqs2_taxonomy_db = '/mnt/project/FjellheimLab/martpali/AnnualPerennial/nf-denovoslim/db/UniProtTrEMBLtaxdb'
    eggnog_annotations  = '/mnt/project/glowberry/transannot/db/e7_as_e5_annotations.tsv'
    busco_lineage       = 'poales_odb12'

    // --- Diamond DB for frameshift correction (pre-built .dmnd, or null to auto-build from MMseqs2 SwissProt) ---
    diamond_db          = null

    // --- Clustering params ---
    mmseqs2_nt_id       = 0.97
    mmseqs2_nt_cov      = 0.8
    mmseqs2_search_sens = 7.0

    // --- Taxonomy filter (NCBI taxon ID — includes all descendants) ---
    filter_taxon        = 35493   // Streptophyta

    // --- TD2 params ---
    td2_min_orf_length  = 90
    td2_strand_specific = true

    // --- Chunking params ---
    search_orf_chunk_size         = 40000  // ORFs per chunk for MMSEQS2_SEARCH
    taxonomy_chunk_size           = 31000  // SuperTranscripts per chunk for MMSEQS2_TAXONOMY
    max_parallel_search_chunks    = 8      // Max simultaneous search jobs
    max_parallel_taxonomy_chunks  = 8      // Max simultaneous taxonomy jobs

    // --- Containers (Docker format — auto-converted for Singularity/Apptainer) ---
    sortmerna_container  = 'community.wave.seqera.io/library/sortmerna:4.3.7--b730cad73fc42b8e'
    salmon_container     = 'quay.io/biocontainers/salmon:1.10.3--h6dccd9a_2'
    mmseqs2_container    = 'quay.io/biocontainers/mmseqs2:15.6f452--pl5321h6a68c12_3'
    corset_container     = 'quay.io/biocontainers/corset:1.09--h077b44d_6'
    lace_container       = "${projectDir}/containers/lace/lace_1.14.1_patched.sif"
    td2_container        = "${projectDir}/containers/td2/td2_1.0.8.sif"
    busco_container      = 'ezlabgva/busco:v6.0.0_cv1'
    transannot_container = 'quay.io/biocontainers/transannot:4.0.0--pl5321hd6d6fdc_2'
    diamond_container    = 'quay.io/biocontainers/diamond:2.1.22--h13889ed_0'
    biopython_container  = 'quay.io/biocontainers/biopython:1.81'

    // --- Resource caps (Orion: 384 CPUs, ~1.5 TB per node) ---
    max_cpus            = 32
    max_memory          = 800.GB
    max_time            = 336.h   // 14 days

    // --- Output ---
    outdir              = './results'
}

process {
    withName: 'SORTMERNA.*'           { container = params.sortmerna_container }
    withName: 'MMSEQS2_.*'            { container = params.mmseqs2_container }
    withName: 'SALMON_.*'             { container = params.salmon_container }
    withName: 'CORSET'                { container = params.corset_container }
    withName: 'LACE'                  { container = params.lace_container }
    withName: 'DIAMOND_BLASTX'          { container = params.diamond_container }
    withName: 'CORRECT_FRAMESHIFTS'      { container = params.biopython_container }
    withName: 'TD2_.*'                { container = params.td2_container }
    withName: 'BUSCO_QC'              { container = params.busco_container }
    withName: 'TRANSANNOT'            { container = params.transannot_container }
    withName: 'SELECT_BEST_ORF'       { container = params.biopython_container }
    withName: 'VALIDATE_IDS'          { container = 'ubuntu:22.04' }
}

// Load base resource config
includeConfig 'conf/base.config'

// --- Profiles ---
//
// Container engines (apptainer, singularity, docker):
//   Enable the container runtime; generic settings only.
//
// Executors (slurm):
//   Generic SLURM scheduling, no site-specific settings.
//
// Site-specific (orion):
//   NMBU Orion HPC peculiarities — queue name, group ownership fix,
//   filesystem bind mounts, scratch disabled for quota reasons.
//
// Usage:  -profile apptainer,orion      (Orion HPC)
//         -profile apptainer,slurm      (generic SLURM cluster)
//         -profile docker               (local Docker)
//
profiles {
    apptainer {
        apptainer.enabled     = true
        apptainer.autoMounts  = true
        apptainer.cacheDir    = "${projectDir}/singularity_cache"
        apptainer.pullTimeout = '45m'
        docker.enabled        = false
    }
    singularity {
        singularity.enabled    = true
        singularity.autoMounts = true
        singularity.cacheDir   = "${projectDir}/singularity_cache"
        docker.enabled         = false
    }
    slurm {
        process.executor         = 'slurm'
        executor.submitRateLimit = '30/1min'
        executor.queueSize       = 20
    }
    orion {
        // NMBU Orion HPC — 6 nodes (cn-31..cn-35, cn-37), 384 CPUs, ~1.5 TB RAM each
        process.executor       = 'slurm'
        process.queue          = 'orion'
        // Exclude nodes with nearly-full local disks (check with cn_quota.sh)
        process.clusterOptions = '--exclude=cn-37'
        // Scratch disabled: mv preserves file ownership (martpali:martpali) instead
        // of group (fjellheimlab), causing personal quota to fill up.
        process.scratch        = false
        // Group ownership fix (two layers):
        //   1. sbatch wrappers run `sg fjellheimlab` so the Nextflow HEAD process
        //      creates work-dir scaffolding under fjellheimlab group.
        //   2. beforeScript sets setgid+umask on each task dir, because SLURM
        //      child jobs revert to the user's default group (martpali).
        process.beforeScript   = '''
            chgrp fjellheimlab . 2>/dev/null || true
            chmod g+s . 2>/dev/null || true
            umask 002
        '''
        // Safety net: catch files that escaped the setgid fix
        // (e.g. apptainer-created tmp files, symlink targets).
        process.afterScript    = '''
            chgrp -R fjellheimlab . 2>/dev/null || true
            chmod -R g+rwX . 2>/dev/null || true
        '''
        executor.submitRateLimit = '30/1min'
        executor.queueSize       = 20
        // Bind-mount Orion filesystems into containers
        apptainer.runOptions   = '-B /mnt/project:/mnt/project -B /net/fs-2/scale:/net/fs-2/scale -B /work:/work'
        singularity.runOptions = '-B /mnt/project:/mnt/project -B /net/fs-2/scale:/net/fs-2/scale -B /work:/work'
    }
    docker {
        docker.enabled    = true
        docker.runOptions = '-u $(id -u):$(id -g)'
    }
}

// --- Manifest ---
manifest {
    name            = 'nf-denovoslim'
    author          = 'Martin Paliocha'
    description     = 'Trinity assembly thinning pipeline'
    mainScript      = 'main.nf'
    nextflowVersion = '!>=23.04.0'
    version         = '0.1.0'
}
