/* nf-denovoslim — nextflow.config */

params {
    // --- Input ---
    trinity_fasta       = null
    samplesheet         = null
    species_label       = 'species_X'

    // --- SortMeRNA rRNA databases ---
    sortmerna_db_urls   = [
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5.8s-database-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5s-database-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-arc-16s-id95.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-arc-23s-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-bac-16s-id90.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-bac-23s-id98.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-euk-18s-id95.fasta',
        'https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/silva-euk-28s-id98.fasta'
    ]
    sortmerna_db_dir    = null   // Pre-downloaded dir; if null, downloads from URLs

    // --- Databases (pre-built MMseqs2 — set via CLI or site config) ---
    mmseqs2_swissprot   = null
    mmseqs2_pfam        = null
    mmseqs2_eggnog      = null
    mmseqs2_taxonomy_db = null
    eggnog_annotations  = null
    busco_lineage       = null       // e.g. 'poales_odb12', 'eudicots_odb12'

    // --- Unix group for shared quota accounting (null = skip group fix) ---
    unix_group          = null
    orion_exclude_nodes = null       // e.g. 'cn-37' (Orion-specific)

    // --- Diamond DB for frameshift correction (pre-built .dmnd, or null to auto-build from MMseqs2 SwissProt) ---
    diamond_db          = null

    // --- Search params ---
    mmseqs2_search_sens = 7.0

    // --- Taxonomy filter (NCBI taxon ID — includes all descendants) ---
    filter_taxon        = 35493   // Streptophyta

    // --- TD2 params ---
    td2_min_orf_length  = 90
    td2_strand_specific = true

    // --- Containers (Docker format — auto-converted for Singularity/Apptainer) ---
    sortmerna_container  = 'community.wave.seqera.io/library/sortmerna:4.3.7--b730cad73fc42b8e'
    salmon_container     = 'quay.io/biocontainers/salmon:1.10.3--h6dccd9a_2'
    mmseqs2_container    = 'quay.io/biocontainers/mmseqs2:18.8cc5c--hd6d6fdc_0'
    corset_container     = 'quay.io/biocontainers/corset:1.09--h077b44d_6'
    lace_container       = "${projectDir}/containers/lace/lace_1.14.1_patched.sif"
    td2_container        = "${projectDir}/containers/td2/td2_1.0.8.sif"
    busco_container      = 'ezlabgva/busco:v6.0.0_cv1'
    transannot_container = 'quay.io/biocontainers/transannot:4.0.0--pl5321hd6d6fdc_2'
    diamond_container    = 'quay.io/biocontainers/diamond:2.1.22--h13889ed_0'
    biopython_container  = 'quay.io/biocontainers/biopython:1.81'

    // --- Resource caps (overridden by site configs / profiles) ---
    max_cpus            = 16
    max_memory          = 128.GB
    max_time            = 168.h   // 7 days

    // --- Output ---
    outdir              = './results'
}

process {
    shell = ['/bin/bash', '-euo', 'pipefail']

    withName: 'SORTMERNA.*'           { container = params.sortmerna_container }
    withName: 'MMSEQS2_.*'            { container = params.mmseqs2_container }
    withName: 'SALMON_.*'             { container = params.salmon_container }
    withName: 'CORSET'                { container = params.corset_container }
    withName: 'LACE'                  { container = params.lace_container }
    withName: 'DIAMOND_BLASTX.*'      { container = params.diamond_container }
    withName: 'CORRECT_FRAMESHIFTS.*' { container = params.biopython_container }
    withName: 'TD2_.*'                { container = params.td2_container }
    withName: 'BUSCO_.*'              { container = params.busco_container }
    withName: 'TRANSANNOT'            { container = params.transannot_container }
    withName: 'SELECT_BEST_ORF'       { container = params.biopython_container }
    withName: 'VALIDATE_IDS'          { container = 'ubuntu:22.04' }
    withName: 'THINNING_REPORT'       { container = params.biopython_container }
}

// Load base resource config
includeConfig 'conf/base.config'

// Usage: -profile apptainer,orion | apptainer,slurm | docker
profiles {
    apptainer {
        apptainer.enabled     = true
        apptainer.autoMounts  = true
        apptainer.cacheDir    = "${projectDir}/singularity_cache"
        apptainer.pullTimeout = '45m'
        docker.enabled        = false
    }
    singularity {
        singularity.enabled    = true
        singularity.autoMounts = true
        singularity.cacheDir   = "${projectDir}/singularity_cache"
        docker.enabled         = false
    }
    slurm {
        process.executor         = 'slurm'
        executor.submitRateLimit = '30/1min'
        executor.queueSize       = 20
    }
    orion {
        // NMBU Orion HPC — loads site-specific config
        // Set params.unix_group and params.orion_exclude_nodes on CLI or in run scripts
        includeConfig 'conf/orion.config'
    }
    docker {
        docker.enabled    = true
        docker.runOptions = '-u $(id -u):$(id -g)'
    }
    // Resource profiles — pick one to match your cluster
    highmem {
        // Large-memory nodes (>512 GB RAM, e.g. Orion)
        params.max_cpus   = 32
        params.max_memory = 1200.GB
        params.max_time   = 336.h
    }
    standard {
        // Mid-range cluster nodes (128-256 GB RAM)
        params.max_cpus   = 16
        params.max_memory = 128.GB
        params.max_time   = 168.h
    }
    test {
        // Minimal test run with low resources
        params.max_cpus   = 4
        params.max_memory = 16.GB
        params.max_time   = 6.h
    }
}

// --- Manifest ---
manifest {
    name            = 'nf-denovoslim'
    author          = 'Martin Paliocha'
    description     = 'Trinity assembly thinning pipeline'
    mainScript      = 'main.nf'
    nextflowVersion = '!>=23.04.0'
    version         = '0.1.0'
}
